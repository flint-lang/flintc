use Core.assert

data Vec2:
    i32 x;
    i32 y;
    Vec2(x, y);

def move_maybe(mut Vec2? v2, i32 x, i32 y):
    if v2 != none:
        v2!.(x, y) = v2!.(x, y) + (x, y);

def equals(i32? lhs, i32? rhs) -> bool:
    return lhs == rhs;

def has_value(i32? opt) -> bool:
    return opt != none;

def set_value(mut i32? opt, i32 val):
    opt = val;

def test_optional_switch():
    i32? maybe = 60;
    switch maybe:
        none: assert(false);
        v:
            assert(v == 60);
            v = 30;
            assert(v == 30);
    assert(maybe! == 30);

    i32 res = switch maybe:
        none -> 0;
        v -> v;
    assert(res == 30);

    maybe = none;
    res = switch maybe:
        none -> 0;
        v -> v;
    assert(res == 0);

def test_optional():
    i32? maybe = 69;
    i32 unwrapped = maybe!;
    assert(unwrapped == 69);

    maybe = none;
    assert(maybe == none);
    assert(not has_value(maybe));

    assert(none == none);
    assert(not (none != none));

    i32? maybe_2 = 69;
    assert(maybe != maybe_2);
    assert(not equals(maybe, maybe_2));

    maybe = 69;
    assert(maybe == maybe_2);

    maybe = none;
    maybe_2 = none;
    assert(maybe == maybe_2);
    assert(not (maybe != maybe_2));

    maybe = 69;
    i32 res = maybe ?? 7;
    assert(res == 69);

    maybe = none;
    res = maybe ?? 7;
    assert(res == 7);

    res = 5 + maybe ?? 7;
    assert(res == 12);

    maybe = 10;
    res = maybe ?? 7 + 8;
    assert(res == 18);

    set_value(maybe, 22);
    assert(maybe != none);
    assert(maybe! == 22);

    Vec2 v = Vec2(10, 20);
    Vec2? vm = v;
    assert(v.x == 10 and v.y == 20);
    assert(vm!.x == 10 and vm!.y == 20);

    move_maybe(vm, 2, 3);
    assert(v.x == 12 and v.y == 23);
    assert(vm!.x == 12 and vm!.y == 23);

    test_optional_switch();
