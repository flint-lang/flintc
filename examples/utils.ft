use Core.system
use Core.assert
use Core.print

/// @brief Checks whether a given string starts with the given other string
///
/// @brief `string` The string to check
/// @brief `check` The string with which the other string may start with
/// @return `bool` Whether the `string` starts with `check`
def starts_with(str string, str check) -> bool:
	if string.length < check.length:
		return false;
	const bool is_ok = string[..check.length] == check;
	if not is_ok:
		print($"expected:\n{check}\n");
		print($"but got:\n{string}\n");
		print("\n");
	return is_ok;

/// @brief Tests the given test file, a test succeeds if it's exit code is 0
///
/// @param `test_dir` The directory the test needs to be tested in
/// @param `file_name` The test file to test
def test_test(str test_dir, str file_name):
	cwd := get_cwd();
	normalized_path := get_path($"{cwd}/{test_dir}");
	str test_file = $"{normalized_path}/{file_name}";
	str out_file = $"{normalized_path}/test";
	(exit_code, output) := system_command($"flintc --file {test_file} --test --out {out_file}");
	if exit_code != 0:
		print(output);
	assert(exit_code == 0);
	assert(output == "");
	(exit_code, output) = system_command(get_path($"{normalized_path}/test"));
	if exit_code != 0:
		print(output);
	assert(exit_code == 0);

/// @brief Compiles a given file and expects compilation to succeed. The compiled program
///        is expected to have the given expected output.
///
/// @param `file` The file to compile
/// @param `expected_output` The expected output of the compiled program
def test_file_ok(str file, str? expected_output):
	(exit_code, output) := system_command($"flintc --file {file}");
	if exit_code != 0:
		print($"Unexpected compiler exit_code: {exit_code}\n");
	assert(exit_code == 0);
	if output != "":
		print($"Unexpected compiler output: '{output}'\n");
	assert(output == "");
	(exit_code, output) = system_command("./main");
	if exit_code != 0:
		print($"Unexpected 'main' exit_code: {exit_code}\n");
	assert(exit_code == 0);
	if expected_output == none:
		return;
	if output != expected_output!:
		print($"Unexpected output:\n'{output}'\n");
		print($"Expected output:\n'{expected_output!}'\n");
	assert(output == expected_output!);

/// @brief Compiles a given file and expects compilation to succeed. The compiled program
///        is expected to have the given expected output.
///
/// @param `path` The path in which to compile and execute the program
/// @param `file` The file to compile
/// @param `expected_output` The expected output of the compiled program, if this is 'none'
///                          then the output is not checked, only the exit code of the
///                          program is checked
def test_file_ok_in(str path, str file, str? expected_output):
    cwd := get_cwd();
    normalized_path := get_path($"{cwd}/{path}");
	(exit_code, output) := system_command($"cd {normalized_path} && flintc --file {file}");
	assert(exit_code == 0);
	assert(output == "");
	(exit_code, output) = system_command($"cd {normalized_path} && {get_path("./main")}");
	assert(exit_code == 0);
	if expected_output == none:
		return;
	if output != expected_output!:
		print($"UNEXPECTED_EXE: output = '{output}'\n");
		print($"EXPECTED_EXE: output = '{expected_output!}'\n");
	assert(output == expected_output!);

/// @brief Compiles a given file and expects compilation to succeed. The compiled program
///        is not executed.
///
/// @param `file` The file to compile
def test_compiles(str file):
	(exit_code, output) := system_command($"flintc --file {file}");
	if exit_code != 0:
		print($"UNEXPECTED_CMP: exit_code = {exit_code}\n");
	assert(exit_code == 0);
	if output != "":
		print($"UNEXPECTED_CMP: output = '{output}'\n");
	assert(output == "");

/// @brief Compiles a given file and expects compilation to succeed. The compiled program
///        is not executed.
///
/// @param `path` The path in which to compile the program
/// @param `file` The file to compile
/// @param `flags` Optional additional flags to compile with (for example `--flags="-lraylib"`)
def test_compiles_in(str path, str file, str? flags):
    cwd := get_cwd();
    normalized_path := get_path($"{cwd}/{path}");
	(exit_code, output) := system_command($"cd {normalized_path} && flintc --file {file} {flags ?? ""}");
	if exit_code != 0:
		print($"UNEXPECTED_CMP: exit_code = {exit_code}\n");
	assert(exit_code == 0);
	if output != "":
		print($"UNEXPECTED_CMP: output = '{output}'\n");
	assert(output == "");

/// @brief Compiles a given file and expects compilation to fail with an expected output
///
/// @param `file` The file to compile
/// @param `expected_output` The expected output of the compiler
def test_file_fail(str file, str expected_output):
	(exit_code, output) := system_command($"flintc --no-colors --file {file}");
	assert(exit_code == 1);
	assert(starts_with(output, expected_output));

/// @brief Executes a given command inside a subdirectory and expects a given output
///
/// @param `path` The path in which to execute the command
/// @param `file` The file to compile
/// @param `expected_output` The expected output of the failing compilation
def test_file_fail_in(str path, str file, str expected_output):
    cwd := get_cwd();
    normalized_path := get_path($"{cwd}/{path}");
	(exit_code, output) := system_command($"cd {normalized_path} && flintc --no-colors --file {file}");
	assert(exit_code == 1);
	assert(starts_with(output, expected_output));

/// @brief Executes a given command inside a subdirectory and expects a given output
///        It skips the first `n` lines of the output and only compares the rest of the output.
///        @asserts that `n` is at least 1
///
/// @param `path` The path in which to execute the command
/// @param `file` The file to compile
/// @param `n` The number of lines to skip before starting to check the output
/// @param `expected_output` The expected output of the failing compilation
def test_file_fail_in(u32 n, str path, str file, str expected_output):
	assert(n >= 1);
	cwd := get_cwd();
    normalized_path := get_path($"{cwd}/{path}");
	(exit_code, output) := system_command($"cd {normalized_path} && flintc --no-colors --file {file}");
	assert(exit_code == 1);
	u64 start_idx = 0;
	u64 lines_skipped = 0;
	for (idx, char) in output:
		if char == '\n':
			lines_skipped++;
			if lines_skipped == n:
				start_idx = idx + 1;
				break;
	assert(starts_with(output[start_idx..], expected_output));

/// @brief Tries to compile the given file, expecting compilation to succeed but the
///        compiled program to crash
///
/// @param `file` The file to compile
/// @param `expected_output` The expected output of the run program
def test_file_crash(str file, str expected_output):
	(exit_code, output) := system_command($"flintc --file {file}");
	assert(exit_code == 0);
	assert(output == "");
	(exit_code, output) = system_command("./main");
	assert(exit_code != 0);
	assert(output == expected_output);
