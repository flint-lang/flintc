use Core.print
use Core.math
use Core.time

use Fip.raylib

use "ball.ft"
use "player.ft"
use "cpu.ft"
use "collisions.ft"

const data Colors:
	Color black = Color(0, 0, 0, 255);
	Color white = Color(190, 190, 190, 255);
	Color gray = Color(100, 100, 100, 255);
	Color green = Color(38, 185, 154, 255);
	Color dark_green = Color(20, 160, 133, 255);
	Color light_green = Color(129, 204, 184, 255);
	Color yellow = Color(243, 213, 91, 255);

def reset_objects(mut Ball ball, mut Cpu p1, mut Player p2):
	ball.reset();
	p1.cpu_reset();
	p2.reset();

def main():
	// Screen
	i32x2 screen = (1280, 800);
	u32 flags = 4; // Enable Screen Resizable mode
	flags += 64; // Enable Vsync
	SetConfigFlags(flags);
	InitWindow(screen.x, screen.y, "Pong-3.0");

	// Objects
	i32x2 player_size = (24, 120);
	Ball ball = Ball(DBall(f32x2(0, 0), f32x2(0, 0), 0.0, 20.0));
	Cpu p1 = Cpu(DPlayer(player_size, f32x2(0, 0), 370.0));
	Player p2 = Player(DPlayer(player_size, f32x2(0, 0), 370.0));
	reset_objects(ball, p1, p2);


	// The scores
	u32 player_score = 0;
	u32 cpu_score = 0;
	TimeStamp last_frame = now();
	while not WindowShouldClose():
		screen = (GetScreenWidth(), GetScreenHeight());
		BeginDrawing();
		ClearBackground(Colors.dark_green);
		DrawRectangle(screen.x / 2, 0, screen.x / 2, screen.y, Colors.green);
		DrawCircle(screen.x / 2, screen.y / 2, 150.0, Colors.light_green);

		// Update the game objects
		TimeStamp current_frame = now();
		Duration frame_duration = duration(last_frame, current_frame);
		f32 delta = f32(as_unit(frame_duration, TimeUnit.S));
		last_frame = current_frame;
		p1.cpu_update(ball.get_y(), delta);
		p2.update(delta);
		ball.update(delta);

		// Check whether somebody has won, and reset the game if one has
		GameState state = check_collisions(ball, p1, p2);
		if state == GameState.P1_WON:
			print("Player 1 Won!\n");
			reset_objects(ball, p1, p2);
			cpu_score++;
		else if state == GameState.P2_WON:
			print("Player 2 Won!\n");
			reset_objects(ball, p1, p2);
			player_score++;

		// Drawing stuff
		DrawLine(screen.x / 2, 0, screen.x / 2, screen.y, Colors.white);
		ball.draw();
		p1.draw();
		p2.draw();

		// Draw UI
		DrawText(str(cpu_score), screen.x / 4, 20, 60, Colors.white);
		DrawText(str(player_score), (screen.x * 3) / 4, 20, 60, Colors.white);
		EndDrawing();

	CloseWindow();
