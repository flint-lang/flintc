use Core.print
use Core.math
use Core.time

use "raylib.ft"
use "ball.ft"
use "player.ft"
use "cpu.ft"
use "collisions.ft"

def reset_objects(mut Ball ball, mut Cpu p1, mut Player p2):
	ball.reset();
	p1.cpu_reset();
	p2.reset();

def main():
	// Colors
	DColor black = DColor(u8(0), u8(0), u8(0), u8(255));
	DColor white = DColor(u8(190), u8(190), u8(190), u8(255));
	DColor gray = DColor(u8(100), u8(100), u8(100), u8(255));
	DColor green = DColor(u8(38), u8(185), u8(154), u8(255));
	DColor dark_green = DColor(u8(20), u8(160), u8(133), u8(255));
	DColor light_green = DColor(u8(129), u8(204), u8(184), u8(255));
	DColor yellow = DColor(u8(243), u8(213), u8(91), u8(255));

	// Screen
	i32x2 screen = (1280, 800);
	u32 flags = 4; // Enable Screen Resizable mode
	flags += 64; // Enable Vsync
	SetConfigFlags(flags);
	InitWindow(screen.x, screen.y, "Pong-3.0");
	SetTargetFPS(60);

	// Objects
	i32x2 player_size = (24, 120);
	Ball ball = Ball(DBall(f32x2(0, 0), f32x2(0, 0), 0.0, 20.0, yellow));
	Cpu p1 = Cpu(DPlayer(player_size, f32x2(0, 0), 370.0, white));
	Player p2 = Player(DPlayer(player_size, f32x2(0, 0), 370.0, white));
	reset_objects(ball, p1, p2);


	// The scores
	u32 player_score = 0;
	u32 cpu_score = 0;
	TimeStamp last_frame = now();
	TimeStamp current_frame = now();
	u32 target_fps = 60;
	f32 target_frametime = 1 / f32(target_fps);
	while not WindowShouldClose():
		current_frame = now();
		Duration frame_duration = duration(last_frame, current_frame);
		f32 delta = f32(as_unit(frame_duration, TimeUnit.S));
		if delta < target_frametime:
			continue;

		screen = (GetScreenWidth(), GetScreenHeight());
		BeginDrawing();
		ClearBackground(dark_green);
		DrawRectangle(screen.x / 2, 0, screen.x / 2, screen.y, green);
		DrawCircle(screen.x / 2, screen.y / 2, 150.0, light_green);

		// Update the game objects
		p1.cpu_update(ball.get_y(), delta);
		p2.update(delta);
		ball.update(delta);

		// Check whether somebody has won, and reset the game if one has
		GameState state = check_collisions(ball, p1, p2);
		if state == GameState.P1_WON:
			print("Player 1 Won!\n");
			reset_objects(ball, p1, p2);
			cpu_score++;
		else if state == GameState.P2_WON:
			print("Player 2 Won!\n");
			reset_objects(ball, p1, p2);
			player_score++;

		// Drawing stuff
		DrawLine(screen.x / 2, 0, screen.x / 2, screen.y, white);
		ball.draw();
		p1.draw();
		p2.draw();

		// Draw UI
		DrawText(str(cpu_score), screen.x / 4, 20, 60, white);
		DrawText(str(player_score), (screen.x * 3) / 4, 20, 60, white);
		EndDrawing();
		last_frame = now();

	CloseWindow();
